scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
library Holder

let uint128_zero = Uint128 0
let true = True

let one_msg =
  fun (msg : Message) =>
    let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

type Error =
  | AzilValidationFailed
  | ClaimRewardsHolderFundsMismatch
  | CompleteWithdrawalHolderFundsMismatch
  | CompleteWithdrawalHolderFundsZero
  | CompleteWithdrawalHolderNoUnbondedStake
  | HolderAlreadyInitialized
  | ZimplValidationFailed

let make_error =
  fun (result: Error) =>
    let result_code =
      match result with
      | AzilValidationFailed                    => "AzilValidationFailed"
      | ClaimRewardsHolderFundsMismatch         => "ClaimRewardsHolderFundsMismatch"
      | CompleteWithdrawalHolderFundsMismatch   => "CompleteWithdrawalHolderFundsMismatch"
      | CompleteWithdrawalHolderFundsZero       => "CompleteWithdrawalHolderFundsZero"
      | CompleteWithdrawalHolderNoUnbondedStake => "CompleteWithdrawalHolderNoUnbondedStake"
      | HolderAlreadyInitialized                => "HolderAlreadyInitialized"
      | ZimplValidationFailed                   => "ZimplValidationFailed"
      end
    in
    { _exception: "Error"; code: result_code }

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract Holder(
  init_azil_ssn_address     : ByStr20,
  init_azil_address         : ByStr20,
  init_zproxy_address       : ByStr20,
  init_zimpl_address        : ByStr20
)

field azil_ssn_address      : ByStr20 = init_azil_ssn_address
field azil_address          : ByStr20 = init_azil_address
field zproxy_address        : ByStr20 = init_zproxy_address
field zimpl_address         : ByStr20 = init_zimpl_address

field tmp_added_funds: Uint128 = uint128_zero

field initialized  : Bool = False

(************************************************************)
(* @dev: Procedures                                         *)
(************************************************************)

procedure ThrowError(err: Error)
  e = make_error err;
  throw e
end

procedure RequireAzil()
  var_azil_address <- azil_address;
  is_azil = builtin eq _sender var_azil_address;
  match is_azil with
  | True  =>
  | False =>
    e = AzilValidationFailed;
    ThrowError e
  end
end

procedure RequireZimpl()
  var_zimpl_address <- zimpl_address;
  is_zimpl = builtin eq _sender var_zimpl_address;
  match is_zimpl with
  | True  =>
  | False =>
    e = ZimplValidationFailed;
    ThrowError e
  end
end

procedure RequireNotInitialized()
  inited <- initialized;
  match inited with
  | False =>
  | True =>
    e = HolderAlreadyInitialized;
    ThrowError e
  end
end


procedure ProxyDelegate(ssnaddr: ByStr20, proxy_contract_addr: ByStr20, amount: Uint128)
  msg = {_tag: "DelegateStake"; _recipient: proxy_contract_addr; _amount: amount; ssnaddr: ssnaddr};
  msgs = one_msg msg;
  send msgs
end

(************************************************************)
(* @dev: Azil actions                                       *)
(************************************************************)

transition WithdrawStakeAmt(amount: Uint128)
  RequireAzil;
  ssnaddr <- azil_ssn_address;
  var_zproxy_address <- zproxy_address;
  msg = {_tag: "WithdrawStakeAmt"; _recipient: var_zproxy_address; _amount: uint128_zero;
  ssnaddr: ssnaddr; amt: amount};
  msgs = one_msg msg;
  send msgs
end

transition WithdrawStakeAmtSuccessCallBack(ssnaddr: ByStr20, amount: Uint128)
  RequireZimpl
  (* nop *)
end

transition CompleteWithdrawal()
  RequireAzil;
  tmp_added_funds := uint128_zero;
  var_zproxy_address <- zproxy_address;
  msg = {_tag: "CompleteWithdrawal"; _recipient: var_zproxy_address; _amount: uint128_zero };
  msgs = one_msg msg;
  send msgs
end

(****************************************************)
(* We assume that Zimpl calls transitions in order: *)
(* 1. Holder->AddFunds()                            *)
(* 2. Holder->CompleteWithdrawalSuccessCallBack()   *)
(*                                                  *)
(* We should accept funds here, according to ZRC-5  *)
(****************************************************)
transition AddFunds()
  RequireZimpl;
  accept;
  tmp_added_funds := _amount;
  e = { _eventname : "AddFunds"; funder: _sender; amount: _amount };
  event e
end

transition CompleteWithdrawalSuccessCallBack(amount: Uint128)
  RequireZimpl;

  (* send funds and callback to Azil *)
  added_funds <- tmp_added_funds;
  o_azil_address <- azil_address;

  (* reset tmp variable *)
  tmp_added_funds := uint128_zero;

  (* check that Zimpl amount are not zero, it should not go here *)
  amount_zero = builtin eq amount uint128_zero;
  match amount_zero with
  | True =>
    e = { _eventname: "CompleteWithdrawalHolderFundsZero" };
    event e;
    e = CompleteWithdrawalHolderFundsZero;
    ThrowError e
  | False =>
  end;

  (* check that Zimpl sent us correct data *)
  amount_eq = builtin eq added_funds amount;
  match amount_eq with
  | False =>
    e = { _eventname: "CompleteWithdrawalHolderFundsMismatch"; amount: amount; added_funds: added_funds };
    event e;
    e = CompleteWithdrawalHolderFundsMismatch;
    ThrowError e
  | True =>
  end;

  (* transfer funds *)
  msg = {_tag : "CompleteWithdrawalSuccessCallBack"; _recipient : o_azil_address;
    _amount : amount
  };
  msgs = one_msg msg;
  send msgs
end

transition CompleteWithdrawalNoUnbondedStakeCallBack(amount: Uint128)
  RequireZimpl;
  e = { _eventname: "CompleteWithdrawalHolderNoUnbondedStake" };
  event e;
  e = CompleteWithdrawalHolderNoUnbondedStake;
  ThrowError e
end

transition ClaimRewards()
  RequireAzil;
  tmp_added_funds := uint128_zero;

  ssnaddr <- azil_ssn_address;
  var_zproxy_address <- zproxy_address;

  msg = {_tag: "WithdrawStakeRewards"; _recipient: var_zproxy_address; _amount: uint128_zero; ssnaddr: ssnaddr};
  msgs = one_msg msg;
  send msgs
end

transition WithdrawStakeRewardsSuccessCallBack(ssnaddr: ByStr20, rewards: Uint128)
  RequireZimpl;
  added_funds <- tmp_added_funds;
  (* reset tmp variable *)
  tmp_added_funds := uint128_zero;

  (* check that Zimpl sent us correct data *)
  amount_eq = builtin eq added_funds rewards;
  match amount_eq with
  | False =>
    e = ClaimRewardsHolderFundsMismatch;
    ThrowError e
  | True =>
    o_azil_address <- azil_address;

    msg = { _tag : "ClaimRewardsSuccessCallBack"; _recipient : o_azil_address; _amount : rewards };
    msgs = one_msg msg;
    send msgs
  end
end

transition ConfirmDelegatorSwap(requestor: ByStr20)
  RequireAzil;

  var_zproxy_address <- zproxy_address;

  msg = {_tag: "ConfirmDelegatorSwap"; _recipient: var_zproxy_address; _amount: uint128_zero; requestor: requestor};
  msgs = one_msg msg;
  send msgs
end

(* Purpose of this transition is one-time min_deleg_stake delegate from Holder after its deploy *)
(* Else Zimpl will not know anything about Holder and will return DelegDoesNotExistAtSSN error *)
transition DelegateStake()
  RequireNotInitialized;
  accept;
  stake_amt = _amount;

  ssnaddr <- azil_ssn_address;
  var_zproxy_address <- zproxy_address;

  ProxyDelegate ssnaddr var_zproxy_address stake_amt;
  initialized := true
end

transition DelegateStakeSuccessCallBack(ssnaddr: ByStr20, amount: Uint128)
  RequireZimpl
  (* nop *)
end
