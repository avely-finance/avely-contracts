scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
library Holder

let uint128_zero = Uint128 0

let one_msg =
  fun (msg : Message) =>
    let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

type Error =
  | CompleteWithdrawalHolderFundsMismatch
  | CompleteWithdrawalHolderFundsZero
  | CompleteWithdrawalHolderNoUnbondedStake

let make_error =
  fun (result: Error) =>
    let result_code =
      match result with
      | CompleteWithdrawalHolderFundsMismatch => Int32 -301
      | CompleteWithdrawalHolderFundsZero => Int32 -302
      | CompleteWithdrawalHolderNoUnbondedStake => Int32 -303
      end
    in
    { _exception: "Error"; code: result_code }

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract Holder(
  init_azil_ssn_address               : ByStr20,
  init_aimpl_address                  : ByStr20,
  init_proxy_staking_contract_address : ByStr20
)
(*  Parameters *)

(* Mutable fields *)
field azil_ssn_address                : ByStr20 = init_azil_ssn_address
field aimpl_address                   : ByStr20 = init_aimpl_address
field proxy_staking_contract_address  : ByStr20 = init_proxy_staking_contract_address

field tmp_added_funds: Uint128 = uint128_zero

(************************************************************)
(* @dev: Procedures                                         *)
(************************************************************)

procedure ThrowError(err: Error)
  e = make_error err;
  throw e
end

(************************************************************)
(* @dev: Admin actions                                      *)
(************************************************************)

transition ChangeAzilSSNAddress(address: ByStr20)
  azil_ssn_address := address;
  e = { _eventname: "ChangeAzilSSNAddress"; address: address };
  event e
end

transition ChangeAimplAddress(address: ByStr20)
  aimpl_address := address;
  e = { _eventname: "ChangeAimplAddress"; address: address };
  event e
end

transition ChangeProxyStakingContractAddress(address: ByStr20)
  proxy_staking_contract_address := address;
  e = { _eventname: "ChangeProxyStakingContractAddress"; address: address };
  event e
end

transition WithdrawStakeAmt(amount: Uint128)
  ssnaddr <- azil_ssn_address;
  proxy_staking_contract_addr <- proxy_staking_contract_address;
  msg = {_tag: "WithdrawStakeAmt"; _recipient: proxy_staking_contract_addr; _amount: uint128_zero;
  ssnaddr: ssnaddr; amt: amount};
  msgs = one_msg msg;
  send msgs
end

transition WithdrawStakeAmtSuccessCallBack(ssnaddr: ByStr20, amount: Uint128)
  (* Should we ignore the staking contract callbacks? *)
end

transition CompleteWithdrawal()
  (* TODO: IsAimpl; ? *)
  tmp_added_funds := uint128_zero;
  proxy_staking_contract_addr <- proxy_staking_contract_address;
  msg = {_tag: "CompleteWithdrawal"; _recipient: proxy_staking_contract_addr; _amount: uint128_zero };
  msgs = one_msg msg;
  send msgs
end

(****************************************************)
(* We assume that Zimpl calls transitions in order: *)
(* 1. Holder->AddFunds()                            *)
(* 2. Holder->CompleteWithdrawalSuccessCallBack()   *)
(*                                                  *)
(* We should accept funds here, according to ZRC-5  *)
(****************************************************)
transition AddFunds()
  (* TODO: IsZimpl; ? *)
  accept;
  tmp_added_funds := _amount;
  e = { _eventname : "Got funds "; funder: _sender; amount: _amount };
  event e
end

transition CompleteWithdrawalSuccessCallBack(amount: Uint128)
  (* TODO: IsZimpl; *)

  (* send funds and callback to Aimpl *)
  added_funds <- tmp_added_funds;
  o_aimpl_address <- aimpl_address;

  (* check that Zimpl amount are not zero, it should not go here *)
  amount_zero = builtin eq amount uint128_zero;
  match amount_zero with
  | True =>
    e = { _eventname: "CompleteWithdrawalHolderFundsZero" };
    event e;
    e = CompleteWithdrawalHolderFundsZero;
    ThrowError e
  | False =>
  end;

  (* check that Zimpl sent us correct data *)
  amount_eq = builtin eq added_funds amount;
  match amount_eq with
  | False =>
    e = { _eventname: "CompleteWithdrawalHolderFundsMismatch"; amount: amount; added_funds: added_funds };
    event e;
    e = CompleteWithdrawalHolderFundsMismatch;
    ThrowError e
  | True =>
  end;

  (* transfer funds *)
  msg = {_tag : "CompleteWithdrawalSuccessCallBack"; _recipient : o_aimpl_address;
    _amount : amount
  };
  msgs = one_msg msg;
  send msgs
end

transition CompleteWithdrawalNoUnbondedStakeCallBack(amount: Uint128)
  (* TODO: IsZimpl; *)
  e = { _eventname: "CompleteWithdrawalHolderNoUnbondedStake" };
  event e;
  e = CompleteWithdrawalHolderNoUnbondedStake;
  ThrowError e
end

