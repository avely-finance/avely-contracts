scilla_version 0

import ListUtils IntUtils

(***************************************************)
(*               Associated library                *)
(***************************************************)
library AZil

type Withdrawal =
  | Withdrawal of Uint128 Uint128

type Error =
  | ReDelegInvalidSSNAddr
  | DelegDoesNotExistAtSSN
  | InvalidTotalAmt
  | DelegHasNoSufficientAmt
  | DelegStakeNotEnough
  | CantFetchRemoteImplAddr
  | CantFetchRemoteBnumReq

let make_error =
  fun (result: Error) =>
    let result_code =
      match result with
      | DelegDoesNotExistAtSSN => Int32 -7
      | DelegHasNoSufficientAmt => Int32 -13
      | DelegStakeNotEnough => Int32 -15
      | InvalidTotalAmt => Int32 -18
      | ReDelegInvalidSSNAddr => Int32 -21
      | CantFetchRemoteImplAddr => Int32 -101
      | CantFetchRemoteBnumReq => Int32 -102
      end
    in
    { _exception: "Error"; code: result_code }

let uint128_to_uint256 : Uint128 -> Uint256 =
  fun (x : Uint128) =>
    let ox256 = builtin to_uint256 x in
      match ox256 with
      | None =>
        (* this never happens, hence we throw a division by zero exception just in case *)
        let zero = Uint256 0 in
        builtin div zero zero
      | Some x256 => x256
      end

let muldiv : Uint128 -> Uint128 -> Uint128 -> Uint128 =
    fun (x : Uint128) =>
    fun (y : Uint128) =>
    fun (z : Uint128) =>
      let x256 = uint128_to_uint256 x in
      let y256 = uint128_to_uint256 y in
      let z256 = uint128_to_uint256 z in
      let x_mul_y256 = builtin mul x256 y256 in
      let res256 = builtin div x_mul_y256 z256 in
      let ores128 = builtin to_uint128 res256 in
      match ores128 with
      | None =>
        (* this must never happen, hence we throw an integer overflow exception *)
        let max_uint128 = Uint128 340282366920938463463374607431768211455 in
        let fourtytwo128 = Uint128 42 in
        builtin mul max_uint128 fourtytwo128
      | Some res128 =>
        res128
      end

let one_msg =
  fun (msg : Message) =>
    let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

let uint128_zero = Uint128 0
let uint128_one = Uint128 1
let uint32_one = Uint32 1

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract AZil(
  init_azil_ssn_address               : ByStr20,
  init_proxy_staking_contract_address : ByStr20,
  init_buffer_address                 : ByStr20,
  init_holder_address                 : ByStr20
)
(*  Parameters *)
(* (owner     : ByStr20) *)

(* Mutable fields *)
field azil_ssn_address                : ByStr20 = init_azil_ssn_address
field proxy_staking_contract_address  : ByStr20 = init_proxy_staking_contract_address
field buffer_address                  : ByStr20 = init_buffer_address
field holder_address                  : ByStr20 = init_holder_address

field totalstakeamount: Uint128 = uint128_zero
field totaltokenamount: Uint128 = uint128_zero

field balances: Map ByStr20 Uint128 = Emp ByStr20 Uint128

(* AddressOfDeleg -> BlockNumber -> Deposit *)
field withdrawal_pending: Map ByStr20 (Map BNum Withdrawal) = Emp ByStr20 (Map BNum Withdrawal)

field bnum_req_remote: Uint128 = Uint128 35000

(* Keeps track of the cycle number when a deleg last buffered deposit its delegate *)
(* AddressOfDeleg -> CycleNumberWhenLastDeposit *)
field last_buf_deposit_cycle_deleg: Map ByStr20 Uint32 = Emp ByStr20 Uint32

(* 10 ZIL expresssed in Qa where 1 ZIL = 10^12 Qa *)
field mindelegstake: Uint128 = Uint128 10000000000000

field lastrewardcycle: Uint32 = uint32_one

(************************************************************)
(* @dev: Procedures                                         *)
(************************************************************)

(************************************************************)
(* @dev: Holder actions                                     *)
(************************************************************)

procedure HolderWithdrawStakeAmt(amount: Uint128)
  holder_addr <- holder_address;
  proxy_staking_contract_addr <- proxy_staking_contract_address;

  msg = {_tag: "WithdrawStakeAmt"; _recipient: holder_addr; _amount: uint128_zero; amount: amount};
  msgs = one_msg msg;
  send msgs
end

(************************************************************)
(* @dev: Buffer actions                                     *)
(************************************************************)

(* @dev : To delegate the stake to the contract through buffer. *)
procedure BufferDelegate(amount: Uint128)
  buffer_addr <- buffer_address;

  msg = {_tag: "DelegateStake"; _recipient: buffer_addr; _amount: amount};
  msgs = one_msg msg;
  send msgs
end

(************************************************************)
(* @dev: aZil actions                                     *)
(************************************************************)

procedure ThrowError(err: Error)
  e = make_error err;
  throw e
end

procedure IsDelegstakeSufficient(amount: Uint128)
  mindelegstake_l <- mindelegstake;
  suffi =  uint128_le mindelegstake_l amount;
  match suffi with
  | True =>
  | False =>
    e = DelegStakeNotEnough;
    ThrowError e
  end
end

procedure IncreaseTotalStakeAmt(amt: Uint128)
  total_stake_amt <- totalstakeamount;
  new_total_amt = builtin add total_stake_amt amt;
  totalstakeamount := new_total_amt
end

procedure IncreaseTotalTokenAmt(amt: Uint128)
  total_token_amt <- totaltokenamount;
  new_total_amt = builtin add total_token_amt amt;
  totaltokenamount := new_total_amt
end

procedure DecreaseTotalStakeAmt(amt: Uint128)
  current_amt <- totalstakeamount;
  valid = uint128_le amt current_amt;
  match valid with
  | True =>
    new_amt = builtin sub current_amt amt;
    totalstakeamount := new_amt
  | False =>
    e = InvalidTotalAmt;
    ThrowError e
  end
end

procedure DecreaseTotalTokenAmt(amt: Uint128)
  current_amt <- totaltokenamount;
  valid = uint128_le amt current_amt;
  match valid with
  | True =>
    new_amt = builtin sub current_amt amt;
    totaltokenamount := new_amt
  | False =>
    e = InvalidTotalAmt;
    ThrowError e
  end
end

procedure TruncateDeleg(deleg: ByStr20)
  delete balances[deleg];
  delete last_buf_deposit_cycle_deleg[deleg]
end

(* newTokens = amount * totaltokenamount / totalstakeamount *)
procedure Delegate(initiator: ByStr20, stake_amt: Uint128)
  IsDelegstakeSufficient stake_amt;

  lrc <- lastrewardcycle;
  last_buf_deposit_cycle_deleg[initiator] := lrc;

  total_token_amt <- totaltokenamount;
  current_stake_amt <- totalstakeamount;

  first_delegation = builtin eq total_token_amt uint128_zero;
  token_amt =
    match first_delegation with
    | True => stake_amt
    | False => muldiv stake_amt total_token_amt current_stake_amt
    end;

  current_token_amount <- balances[initiator];
  match current_token_amount  with
  | Some current_token_amt =>
    new_current_token_amt = builtin add current_token_amt token_amt;
    balances[initiator] := new_current_token_amt
  | None =>
    balances[initiator] := token_amt
  end;

  IncreaseTotalStakeAmt stake_amt;
  IncreaseTotalTokenAmt token_amt
end

procedure AdjustDeleg(deleg: ByStr20, total_amount: Uint128, withdraw_amount: Uint128)
  sufficient = uint128_le withdraw_amount total_amount;
  match sufficient with
  | True =>
    need_truncate =  builtin eq withdraw_amount total_amount;
    match need_truncate with
    | True =>
      (* Remove all info map recorded for this deleg *)
      TruncateDeleg deleg
    | False =>
      (* Readjust all rest deleg to this cycle *)
      rest_deleg = builtin sub total_amount withdraw_amount;
      (* rest delegate should also meet mindelegstake *)
      IsDelegstakeSufficient rest_deleg;
      TruncateDeleg deleg;
      (* Refer to transition TruncateDeleg, here we don't use Fillin transition because we *)
      (* know they are all empty, and this can reduce some gasfee *)
      balances[deleg] := rest_deleg
    end
  | False =>
    e = DelegHasNoSufficientAmt;
    ThrowError e
  end
end

procedure WithdrawalStakeAmt(initiator: ByStr20, withdraw_token_amt: Uint128)
  current_token_amount <- balances[initiator];
  azil_ssn_addr <- azil_ssn_address;

  match current_token_amount with
  | Some current_token_amt =>
    AdjustDeleg initiator current_token_amt withdraw_token_amt;

    (* Illegal withdraw amount could be handled in procedure AdjustDeleg *)
    (* Calculate token to zil to withdraw *)
    total_token_amt <- totaltokenamount;
    total_stake_amt <- totalstakeamount;
    withdraw_stake_amt = muldiv withdraw_token_amt total_stake_amt total_token_amt;

    withdrawal_bnum <- & BLOCKNUMBER;
    withdraw_pending_o <- withdrawal_pending[initiator][withdrawal_bnum];

    (* Upsert pending withdrawal *)
    match withdraw_pending_o with
    | Some (Withdrawal token stake) =>
      new_withdraw_token_amt = builtin add token withdraw_token_amt;
      new_withdraw_stake_amt = builtin add stake withdraw_stake_amt;
      pending_withdrawal = Withdrawal new_withdraw_token_amt new_withdraw_stake_amt;
      withdrawal_pending[initiator][withdrawal_bnum] := pending_withdrawal
    | None =>
      pending_withdrawal = Withdrawal withdraw_token_amt withdraw_stake_amt;
      withdrawal_pending[initiator][withdrawal_bnum] := pending_withdrawal
    end;

    e = { _eventname: "WithdrawStakeAmt"; ssn_addr: azil_ssn_addr; deleg_address: initiator; withdraw_amount:withdraw_token_amt; withdraw_stake_amount:withdraw_stake_amt };
    event e;

    (* Subtract withdrawn amounts from total counters *)
    DecreaseTotalStakeAmt withdraw_stake_amt;
    DecreaseTotalTokenAmt withdraw_token_amt;

    (* call Holder contract *)
    HolderWithdrawStakeAmt withdraw_stake_amt
  | None =>
    e = DelegDoesNotExistAtSSN;
    ThrowError e
  end
end

(************************************************************)
(* @dev: Admin actions                                      *)
(************************************************************)

procedure FetchRemoteBnumReq()
  proxy_staking_contract_addr <- proxy_staking_contract_address;
  (* Address type cast *)
  (* TODO: it's possible to save gas, using type check of transition parameter ? *)
  (* https://scilla.readthedocs.io/en/latest/scilla-in-depth.html?highlight=remote#dynamic-typecheck-of-addresses *)
  proxyAddrTypeTest <- & proxy_staking_contract_addr as ByStr20 with contract field implementation : ByStr20 end;
  match proxyAddrTypeTest with
  | Some v =>
    implAddr <- & v.implementation;
    implAddrTypeTest <- & implAddr as ByStr20 with contract field bnum_req : Uint128 end;
    match implAddrTypeTest with
    | Some z =>
      bnum_req <- & z.bnum_req;
      bnum_req_remote := bnum_req
    | None =>
        e = CantFetchRemoteBnumReq;
        ThrowError e
    end
  | None =>
    e = CantFetchRemoteImplAddr;
    ThrowError e
  end
end

transition ChangeHolderAddress(address: ByStr20)
  holder_address := address;

  e = { _eventname: "ChangeHolderAddress"; address: address };
  event e
end

transition ChangeBufferAddress(address: ByStr20)
  buffer_address := address;

  e = { _eventname: "ChangeBufferAddress"; address: address };
  event e
end

transition ChangeProxyContractAddress(address: ByStr20)
  proxy_staking_contract_address := address;

  e = { _eventname: "ChangeProxyContractAddress"; address: address };
  event e
end

transition IncreaseTotalStakeAmount(amount: Uint128)
  (* TODO: should we send ZIL here and accept, or just increase by input parameter? *)
  IncreaseTotalStakeAmt amount;

  e = { _eventname: "IncreaseTotalStakeAmount"; amount: amount };
  event e
end

(* @dev: Set the staking parameters of contract. *)
(* @param min_deleg_stake: New mindelegstake value *)
transition UpdateStakingParameters(min_deleg_stake: Uint128)
  mindelegstake := min_deleg_stake;

  e = { _eventname: "UpdateStakingParameters"; min_deleg_stake: min_deleg_stake };
  event e
end


transition UpdateLastRewardCycle(last_reward_cycle: Uint32)
  lastrewardcycle := last_reward_cycle;

  e = { _eventname: "UpdateLastRewardCycle"; last_reward_cycle: last_reward_cycle };
  event e
end

(************************************************************)
(* @dev: Deposit undelegated zil, use our node              *)
(************************************************************)
transition DelegateStake()
  accept;
  stake_amt = _amount;
  initiator = _sender;

  BufferDelegate stake_amt;
  Delegate initiator stake_amt;

  e = { _eventname: "DelegateStake"; delegator: initiator; amount: stake_amt };
  event e;

  msg_to_delegator = {_tag : "DelegateStakeSuccessCallBack"; _recipient : initiator; _amount : uint128_zero; amount : stake_amt};
  msg = one_msg msg_to_delegator;
  send msg
end

(* @param amount: Amount of tokens to withdraw, ZIL stake amount to withdraw will be calculated basing on this parameter *)
transition WithdrawStakeAmt(amount: Uint128)
  initiator = _sender;
  azil_ssn_addr <- azil_ssn_address;

  (* first we do all checks, then call Holder on success within procedure*)
  WithdrawalStakeAmt initiator amount;

  msg_to_delegator = {_tag : "WithdrawStakeAmtSuccessCallBack"; _recipient : initiator; _amount : uint128_zero;
  ssnaddr : azil_ssn_addr; amount : amount};
  msg = one_msg msg_to_delegator;
  send msg
end

(************************************************************)
(* @dev: Returns an account zil amount                      *)
(************************************************************)
transition ZilBalanceOf(address: ByStr20)
  total_token_amt <- totaltokenamount;
  total_stake_amt <- totalstakeamount;

  current_token_amount <- balances[address];

  balance =
    match current_token_amount with
    | Some current_token_amt => muldiv current_token_amt total_stake_amt total_token_amt
    | None => uint128_zero
    end;

  e = { _eventname: "ZilBalanceOf"; address: address; balance: balance };
  event e;

  msg_to_sender = { _tag : "ZilBalanceOfCallBack"; _recipient : _sender; _amount : uint128_zero; address: address; balance : balance};
  msgs = one_msg msg_to_sender;
  send msgs
end
